option, -info;
set, format=".16e";

!-- user settings
deltap = 1e-3; ! must be modified in the script, not here...
ncell  = 10;
lcell  = 10;

!---------------------------------------
!-- make sequence and beam
!---------------------------------------

beam, particle=positron, energy=2;

mb: sbend,      l=2, angle=pi/ncell; ! 2 mb per fodo cell
mq: quadrupole, l=1;
ms: sextupole,  l=0.5;

b1: mb, k0=mb->angle/mb->l;
b2: mb, k0=mb->angle/mb->l;
qf: mq, k1= 0.3;
qd: mq, k1=-0.3;
sf: ms, k2=0;
sd: ms, k2=0;

value, ncell*lcell;

sequ: sequence, l=ncell*lcell, refer=entry;
i = 0;
while (i < ncell) {
  qf, at=0+i*lcell ;
  sf, at=1+i*lcell ;
  b1, at=2+i*lcell ;

  qd, at=5+i*lcell ;
  sd, at=6+i*lcell ;
  b2, at=7+i*lcell ;
  i = i+1;
}
endsequence;

!---------------------------------------
!-- study starts here
!---------------------------------------

!--- use sequence
use, sequence=sequ;

!-- initial conditions
coguess, x=-1e-3, px=1e-4, y=-1e-4, py=1e-3, t=0, pt=0;

!-- columns selection
select, flag=twiss, column={name, s, x, px, y, py, t, pt, betx, bety, mux, muy, alfx, alfy, dx, dpx, dy, dpy};
select, flag=ptc_twiss, column={name, s, x, px, y, py, t, pt, betx, bety, mu1, mu2, alfx, alfy, disp1, disp2, disp3, disp4};
select, flag=survey, column={name, s, l, angle, tilt, x, y, z, theta, phi, psi};

!-- check geometry
survey, file="ex_ref/survey_x.ref";

!-- legacy twiss
twiss, file="ex_ref/twiss0_x.ref", deltap=1e-3, betx=1, bety=1; ! no closed orbit
twiss, file="ex_ref/twiss1_x.ref", deltap=1e-3, chrom;          ! do closed orbit

!-- ptc twiss
ptc_create_universe ;
ptc_setswitch, debuglevel=1, mapdump=0;
ptc_create_layout, model=2, method=4, nst=1, time=true, exact=true ;
ptc_twiss, closed_orbit=false, no=1, icase=5, file="ex_ref/twiss0_p.tfs", deltap=1e-3,
          x=-1e-3, px=1e-4, y=-1e-4, py=1e-3, t=0, pt=0, betx=1, bety=1, betz=1 ;
ptc_twiss, closed_orbit=true , no=5, icase=5, file="ex_ref/twiss1_p.ref", deltap=1e-3,
           x=-1e-3, px=1e-4, y=-1e-4, py=1e-3, t=0, pt=0 ;
ptc_end ;

! stop;

!---------------------------------------
!-- match tunes and chromaticities
!---------------------------------------

match, chrom;
vary, name = qf->k1;
vary, name = qd->k1;
vary, name = sf->k2;
vary, name = sd->k2;
global, q1  = 2.34;
global, q2  = 2.29;
global, dq1 = 5.0;
global, dq2 = 5.0;
lmdif, tolerance=1e-21;
endmatch;

! BUG in MAD-X, deltap cannot be used as variable to init twiss...
twiss, file="ex_ref/twiss_match_x.ref", deltap=1e-3, chrom;

ptc_create_universe ;
ptc_create_layout, model=2, method=4, nst=1, time=true, exact=true ;
ptc_twiss, closed_orbit=true, no=1, icase=5, file="ex_ref/twiss_match_p.ref", deltap=1e-3,
           x=-1e-3, px=1e-4, y=-1e-4, py=1e-3, t=0, pt=0 ;
ptc_end ;

removefile, file="internal_mag_pot.txt";

