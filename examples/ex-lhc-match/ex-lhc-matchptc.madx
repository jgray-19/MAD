! time ../madx64 ex-lhc-matchptc.madx > ex-lhc-matchptc.out 2>&1

! Links definitions
option, warn,info;
set, format=".16e";

option, -echo,-warn,-info;

call, file="ex_ref/lhc_as-built.seq";
call, file="ex_ref/opt_400_10000_400_3000.madx";

beam, sequence=lhcb1, energy=nrj, particle=proton;
use, sequence=lhcb1;

option, warn;

select,flag=twiss,clear;
select,flag=twiss, pattern=ip, column=s, name, betx, bety, alfx, alfy;
twiss,chrom,table, file="lhcb1_twiss_x.tfs";

!!!!!!!!!!!!!!!!!!!!
print, text="STARTING TUNE MATCHING";
system, "date";
ncall1=0;

match, use_macro;
 vary, name=kqtf.b1, step=1.0E-7 ;
 vary, name=kqtd.b1, step=1.0E-7 ;
 m1: macro = {
   ncall1 = ncall1+1 ;
   ptc_create_universe ;
   ptc_create_layout, model=2, method=4, nst=1, time=true, exact=true ;
   !ptc_setswitch, fringe=true, time=true, totalpath=false ;
   ptc_twiss, closed_orbit=true, no=1, icase=5, summary_table ;
   ptc_end ;
 }
 constraint, expr = table(ptc_twiss_summary,q1)=0.295 ;
 constraint, expr = table(ptc_twiss_summary,q2)=0.301 ;
 !jacobian, calls=100, tolerance=1E-20 ;
 lmdif, calls=100, tolerance=1E-20 ;
endmatch ;

print, text="END OF TUNE MATCHING";
system, "date";
value, ncall1;

!!!!!!!!!!!!!!!!!!!!
print, text="STARTING CHROMA MATCHING";
system, "date";
ncall2=0;

match, use_macro ;
 vary, name=ksf.b1, step=1E-7 ;
 vary, name=ksd.b1, step=1E-7 ;
 m1: macro = {
   ncall2 = ncall2+1 ;
   ptc_create_universe ;
   ptc_create_layout, model=2, method=4, nst=1, time=true, exact=true ;
   !ptc_setswitch, fringe=true, time=true, totalpath=false ;
   ptc_twiss, closed_orbit=true, no=2, icase=5, summary_table ;
   ptc_end ;
 }
 constraint, expr = table(ptc_twiss_summary,dq1)=15 ;
 constraint, expr = table(ptc_twiss_summary,dq2)=15 ;
 !jacobian, calls=100, tolerance=1E-16 ;
 lmdif, calls=100, tolerance=1E-20 ;
endmatch ;

print, text="END OF CHROMA MATCHING";
system, "date";
value, ncall1, kqtf.b1, kqtd.b1;
value, ncall2, ksf.b1 , ksd.b1 ;

removefile, file="internal_mag_pot.txt";

exit;

!!!!!!!!!!!!!!!!!!!!
PTC results: LMDIF
ncall1             =  1.1000000000000000e+01 ;
kqtf.b1            = -7.1087562926718266e-05 ;
kqtd.b1            =  7.8487956058648450e-05 ;
ncall2             =  1.1000000000000000e+01 ;
ksf.b1             =  1.3095064228638545e-01 ;
ksd.b1             = -2.1684709191589108e-01 ;
real  3m12.306s

PTC results: JACOBIAN (bisec=3)
ncall1             =  1.8000000000000000e+01 ;
kqtf.b1            = -7.1087562882725733e-05 ;
kqtd.b1            =  7.8487956131257226e-05 ;
ncall2             =  2.5000000000000000e+01 ;
ksf.b1             =  1.3095064227009706e-01 ;
ksd.b1             = -2.1684709186459150e-01 ;
real  6m19.545s
